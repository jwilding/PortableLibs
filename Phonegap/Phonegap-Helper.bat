@ECHO OFF
CALL "%~d0\Libs\Setup.bat"

::SET CURRENT_FILE=%0
::SET CURRENT_DRIVE=%~d0
CHCP 437 > NUL

TITLE PhoneGap Helper


:START
CALL :INIT
CALL :MAINMENU_INIT
CLS
GOTO :MAINMENU


:MAINMENU
CALL :MAINMENU_TEXT
CALL :PARSE :MAINMENU_COMMAND
GOTO :MAINMENU


:MAINMENU_INIT

ECHO INITIALIZING MENU

CALL :GENERATESEPARATOR _ " " 114
CALL :GENERATESEPARATOR - "Ä" 114
CALL :GENERATESEPARATOR # "Û" 114
CALL :GENERATESEPARATOR @ "±" 114

CALL :MENUITEMLABEL PROJECT	  		"PROJECT:       !_PROJECT_NAME!"
CALL :MENUITEMLABEL APK_MODE	  	"MODE:          !_APK_MODE!"
CALL :MENUITEMLABEL DIRECTORY		"DIRECTORY:     "!__CD__!""
CALL :MENUITEMLABEL APK	  			"APK:           !_APK!"
CALL :MENUITEMLABEL APK_ZIP	  		"ZIP:           !_APK_ZIP!"
CALL :MENUITEMLABEL APK_PACKAGE	  	"PACKAGE:       !_APK_PACKAGE!"
CALL :MENUITEMLABEL APK_ACTIVITY	"ACTIVITY:      !_APK_ACTIVITY!"
CALL :MENUITEMLABEL APK_SIZE		"APK SIZE:      !_APK_SIZE!"
CALL :MENUITEMLABEL APK_ZIP_SIZE	"ZIP SIZE:      !_APK_ZIP_SIZE!"
CALL :MENUITEMLABEL PGB_ID			"PHONEGAP ID:   !_PGB_ID!"

CALL :MENUITEM EMULATOR			E "Start Emulator"
CALL :MENUITEM LOGAPK			L "Start Log APK"
CALL :MENUITEM LOGERROR			X "Start Log Error                (Useful if APK is crashing)"
CALL :MENUITEM SETDIR			S "Set Current Dir"
CALL :MENUITEM SETAPK			A "Set APK"
CALL :MENUITEM SETMODE			M "Set Mode                       (Debug/Release)"
CALL :MENUITEM BUILDRES			B "Build Resources                (Builds the splash and icons)"
CALL :MENUITEM COMPRESS			Z "PHONEGAP - Compress            (Not Required)"
CALL :MENUITEM UPLOAD			U "PHONEGAP - Upload              (Zips and Uploads)"
CALL :MENUITEM DOWNLOAD			D "PHONEGAP - Download"
CALL :MENUITEM DOWNLOAD_WAIT	W "PHONEGAP - Wait For Build      (Not Required)"
CALL :MENUITEM INITPLATFORM		6 "LOCAL    - Initialize Platform"
CALL :MENUITEM INITPLUGINS		7 "LOCAL    - Initialize Plugins  (Not Required)"
CALL :MENUITEM BUILDAPK			8 "LOCAL    - Build APK"
CALL :MENUITEM SERVEAPK			1 "LIVE     - Serve APK           (For the Phonegap App)"
CALL :MENUITEM INSTAPK			9 "Install APK"
CALL :MENUITEM RUNAPK			0 "Run APK"
CALL :MENUITEM PROMPT			P "Command Prompt"
CALL :MENUITEM GITGUI			G "Git GUI"
CALL :MENUITEM HELP				H "Help"
CALL :MENUITEM RESTART			R "Restart"
CALL :MENUITEM QUIT				Q "Quit"
CALL :MENUITEM COMMAND			C "Run Command"
CALL :MENUITEM INFORMATION		I "Detailed Information"

CALL :MENUITEM ADVANCED			/ "Switch Mode (!_MAINMENU_LIST_MODE_NEXT!)"
CALL :MENUITEM BASIC			/ "Switch Mode (!_MAINMENU_LIST_MODE_NEXT!)"

SET _MAINMENU_LIST_INFO="PROJECT APK_PACKAGE APK_ACTIVITY PGB_ID APK_MODE _ DIRECTORY APK APK_ZIP _ APK_SIZE APK_ZIP_SIZE"

SET _MAINMENU_LIST_BASIC="# # _ PROJECT PGB_ID APK_MODE _ DIRECTORY _ ADVANCED INFORMATION _ - _ SETMODE _ - _ LOGAPK LOGERROR _ - _ BUILDRES _ UPLOAD DOWNLOAD _ SERVEAPK _ INITPLATFORM BUILDAPK _ INSTAPK RUNAPK _ - _ GITGUI _ - _ HELP QUIT _ @ _"
SET _MAINMENU_LIST_ADVANCED="# # _ PROJECT APK_PACKAGE APK_ACTIVITY PGB_ID APK_MODE _ DIRECTORY APK APK_ZIP _ APK_SIZE APK_ZIP_SIZE _ BASIC _ - _ SETDIR SETAPK SETMODE _ - _ EMULATOR LOGAPK LOGERROR _ - _ BUILDRES _ COMPRESS UPLOAD DOWNLOAD_WAIT DOWNLOAD _ SERVEAPK _ INITPLATFORM INITPLUGINS BUILDAPK _ INSTAPK RUNAPK _ - _ COMMAND PROMPT _ GITGUI _ - _ HELP RESTART QUIT _ @ _"

SET _MAINMENU_LIST_MODE_NEXT=BASIC
SET _MAINMENU_LIST_CURRENT=%_MAINMENU_LIST_ADVANCED%
GOTO :EOF


:MAINMENU_TEXT
CALL :MENUITEMPRINT %_MAINMENU_LIST_CURRENT%
GOTO :EOF

:MAINMENU_COMMAND

::SET "VAL=_COMMAND_%1"
::CALL SET "VAL=%%%VAL%%%"

::CALL :MENUITEMPRINT %VAL% & ECHO. & CALL :%VAL%
::SET VAL=
CALL :MENUITEMCHECK %_MAINMENU_LIST_CURRENT% %1
GOTO :EOF


:INFORMATION
CALL :MENUITEMPRINT %_MAINMENU_LIST_INFO%
GOTO :EOF

:ADVANCED
ECHO SETTING ADVANCED
SET _MAINMENU_LIST_MODE_NEXT=BASIC
SET _MAINMENU_LIST_CURRENT=%_MAINMENU_LIST_ADVANCED%
GOTO :EOF

:BASIC
ECHO SETTING BASIC
SET _MAINMENU_LIST_MODE_NEXT=ADVANCED
SET _MAINMENU_LIST_CURRENT=%_MAINMENU_LIST_BASIC%
GOTO :EOF


:PARSE
CALL :MENUITEMPRINT "_ _ @"
CALL CursorPos.exe 1,-3
SET _QUERY=
SET /P _QUERY=Input: 
CALL :MENUITEMPRINT "_ @ _"

IF "%_QUERY%"=="" GOTO :EOF
IF NOT "%_QUERY:~0,1%"=="-" GOTO :PARSEONE
SET "_QUERY=%_QUERY:~1%"

:PARSELOOP
IF "%_QUERY%"=="" GOTO :PARSEEND
CALL %1 %_QUERY:~0,1%
SET "_QUERY=%_QUERY:~1%"
IF NOT "%_QUERY%"=="" CALL :MENUITEMPRINT "_ - _"
GOTO :PARSELOOP

:PARSEONE
CALL %1 %_QUERY%

:PARSEEND

CALL :MENUITEMPRINT "_"
SET _QUERY=
GOTO :EOF





:MENUITEMLABEL
SET "_%1_TEXT= %~2"
GOTO :EOF

:MENUITEM
::%1 = ID; %2 = Command; %3 = Text
SET "_%1_COMMAND=%2"
SET "_%1_TEXT= (%2) %~3"
::SET "_COMMAND_%2=%1"
GOTO :EOF


:MENUITEMPRINT
SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
FOR %%I IN (%~1) DO (
	FOR /F "eol=;delims=" %%A IN ("!_%%I_TEXT!") DO (
		ECHO.%%~A
	)
)
ENDLOCAL
GOTO :EOF

:MENUITEMCHECK
SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION
FOR %%I IN (%~1) DO (
	IF /I "!_%%I_COMMAND!"=="%2" (
		ENDLOCAL
		CALL :MENUITEMPRINT %%I & ECHO. & GOTO :%%I
	)
)
ECHO UNKNOWN COMMAND "%2"
ENDLOCAL
GOTO :EOF


:MENUSEPARATOR
CALL :MENUITEMPRINT "_ - _"
GOTO :EOF

:GENERATESEPARATOR
SET "VAL=%~2"
FOR /L %%A IN (1,1,%3) DO CALL SET "VAL=%%VAL%%%~2"
SET "_%~1_TEXT=%VAL%"
SET VAL=
GOTO :EOF






:UNSET
FOR %%I IN (%~1) DO SET "%%I="
GOTO :EOF



:INIT

ECHO.
ECHO INITIALIZING
ECHO.

CHOICE /C NY /N /T 5 /D N /M "Release Mode    (Y/N): "
SET _B_DEB=%ERRORLEVEL%

CHOICE /C YN /N /T 5 /D N /M "Build Resources (Y/N): "
SET _B_RES=%ERRORLEVEL%

CALL :UNSET "_PROJECT_NAME _APK _APK_ZIP _APK_PACKAGE _APK_ACTIVITY _APK_SIZE _APK_ZIP_SIZE _APK_MODE _PGB_ID"

::ECHO.
::CALL :COPYOVERRIDES
ECHO.
CALL :SETMODE_VAL %_B_DEB%
ECHO.
IF %_B_RES%==1 ( CALL :BUILDRES )

CALL :UNSET "_B_RES _B_DEB"
GOTO :EOF



:SETMODE_VAL
IF %1==1 ( SET "_APK_MODE=debug" ) ELSE ( SET "_APK_MODE=release" )
CALL :UPDATE_NAME
GOTO :EOF

:SETMODE
ECHO (D) Debug
ECHO (R) Release
CHOICE /C DR /N /M "Input: "
CALL :SETMODE_VAL %ERRORLEVEL%
GOTO :EOF



:UPDATE_NAME

FOR /F "delims=" %%A IN ('sed -n -e "/^\s*<name>/s/^\s*<name>\s*\([^<]*\)\s*<\/name>\s*$/\1/p" config.xml') DO (
	SET "_PROJECT_NAME=%%A"
)

SET "_APK=%__CD__%%_PROJECT_NAME: =%"
SET _APK_ZIP="%_APK%.zip"
SET _APK="%_APK%-%_APK_MODE%.apk"

CALL :UPDATE_APK
CALL :UPDATE_ID
GOTO :EOF


:UPDATE_APK
CALL :COMMA %_APK% _APK_SIZE
CALL :COMMA %_APK_ZIP% _APK_ZIP_SIZE

CALL :UNSET "_APK_PACKAGE _APK_ACTIVITY"

IF NOT EXIST %_APK% GOTO :EOF

SET "_APK_SED=aapt dump badging %_APK% ^| sed -n -e"
SET _APK_PACKAGE_PATTERN="/^package: /s/.*name='\([^']*\)'.*$/\1/p"
SET _APK_ACTIVITY_PATTERN="/^launchable-activity:/s/.*name='\([^']*\)'.*$/\1/p"

FOR /F %%A IN ('%_APK_SED% %_APK_PACKAGE_PATTERN%')  DO SET "_APK_PACKAGE=%%A"
FOR /F %%A IN ('%_APK_SED% %_APK_ACTIVITY_PATTERN%') DO SET "_APK_ACTIVITY=%%A"

CALL :UNSET "_APK_SED _APK_PACKAGE_PATTERN _APK_ACTIVITY_PATTERN"

GOTO :EOF


:UPDATE_ID
IF NOT "%_PGB_ID%"=="" GOTO :EOF
FOR /F %%A IN ('CALL pgbuild list ^<^&1 2^> NUL ^| sed -n -e "/^id:.*%_PROJECT_NAME%/s/^id:\s*\([0-9]*\).*$/\1/p"') DO (SET "_PGB_ID=%%A")
IF NOT "%_PGB_ID%"=="" GOTO :EOF
CALL pgbuild list
GOTO :EOF


:COMMA
SET "VALUE=%~z1"
IF "%VALUE%"=="" (
	SET "%2="
	GOTO :COMMAEND
)
SET "%2= Bytes"
:COMMALOOP
CALL SET "%2=%VALUE:~-3%%%%2%%"
SET "VALUE=%VALUE:~0,-3%"
IF "%VALUE%"=="" GOTO :COMMAEND
CALL SET "%2=,%%%2%%"
GOTO :COMMALOOP
:COMMAEND
CALL :UNSET "VALUE"
GOTO :EOF





:EMULATOR
ECHO RUN EMULATOR
START "" "%ANDROID_HOME%\AVD Manager.exe"
::SET /P AVD=Enter device name: 
::START "Emulator" %EMULATOR% -avd %AVD%
GOTO :EOF

:LOGAPK
ECHO START   LOG APK
START "Log CordovaLog" /MAX adb logcat CordovaLog:D *:S
GOTO :EOF

:LOGERROR
ECHO START   LOG ERROR
START "Log Error" /MAX adb logcat *:E
GOTO :EOF

:SETDIR
ECHO SET DIR
SETLOCAL
SET /P DIR=Enter Project Directory: 
CD %DIR%
ENDLOCAL
CALL :UPDATE_APK
GOTO :EOF

:SETAPK
ECHO SET APK
SET /P _APK=Drag apk file here: 
CALL :UPDATE_APK
GOTO :EOF


:COMPRESS
IF EXIST %_APK_ZIP% ( DEL /S /Q %_APK_ZIP% & ECHO. )
ECHO COMPRESSING
ECHO.
ECHO "-x!"  means exclude
ECHO "-xr!" means exclude recursively
ECHO.
SETLOCAL
SET "INCLUDE_FILES=config.xml www\config.xml res\ www\"
SET "EXCLUDE_FILES=-xr^!*.db -x^!res\screen.png"
::" -x^!res\ios\*"
::SET "EXCLUDE_FILES=%EXCLUDE_FILES% -x^!www\audio\ -x^!www\images\dictionary\"
ECHO INCLUDING: %INCLUDE_FILES% %EXCLUDE_FILES%
CALL 7za a -tzip %_APK_ZIP% -mx9 %INCLUDE_FILES% %EXCLUDE_FILES% > NUL
ENDLOCAL
CALL :UPDATE_APK
GOTO :EOF


:UPLOAD_PG
ECHO.
ECHO UPLOADING
ECHO.
CALL pgbuild update %_PGB_ID% %_APK_ZIP%
ECHO.
ECHO COMPLETED
GOTO :EOF


:UPLOAD
CALL :UPDATE_ID
CALL :COMPRESS
CALL :UPLOAD_PG
ECHO COMPLETED
GOTO :EOF


:DOWNLOAD_WAIT
FOR /F "delims=" %%A IN ('CALL pgbuild buildstatus %_PGB_ID% ^| sed -n "/^\s*pending:.*android.*/p"') DO (
	ECHO Waiting 5 seconds for build to be ready
	TIMEOUT /T 5 > NUL
	GOTO :DOWNLOAD_WAIT
)
ECHO.
ECHO DOWNLOAD READY
GOTO :EOF

:DOWNLOAD
CALL :UPDATE_ID
CALL :DOWNLOAD_WAIT
ECHO.
ECHO DOWNLOADING
ECHO.
CALL pgbuild download %_PGB_ID% android
ECHO.
ECHO COMPLETED
CALL :UPDATE_APK
GOTO :EOF


:COPYOVERRIDES
ECHO COPYING OVERRIDES
ECHO.
CALL XCOPY "Libs overrides\*" "%CURRENT_DRIVE%\Libs\" /S /Y
GOTO :EOF

:BUILDRES
ECHO BUILDING RESOURCES
ECHO.
CALL cordova-gen
GOTO :EOF


:INITPLUGINS
ECHO INITIALIZE PLUGINS
ECHO.
SETLOCAL
SET PATTERN="/^\s*<gap:plugin /s/.*name=['""]\([^'""]*\)['""].*$/\1/gp"
FOR /F %%A IN ('sed -n -e %PATTERN% config.xml') DO (
	ECHO DOWNLOADING: %%A
	ECHO.
	CALL phonegap plugin add %%A
)
ENDLOCAL
GOTO :EOF


:INITPLATFORM
ECHO INITIALIZE PLATFORM
ECHO.

IF "%CD%"=="%ANDROID_HOME%" (
	ECHO INCORRECT PROJECT DIR
	GOTO :EOF
)

IF EXIST "platforms\" ( RD /S /Q "platforms\" )
IF EXIST "plugins\" ( RD /S /Q "plugins\" )

ECHO ADDING ANDROID PLATFORM
ECHO.
CALL phonegap platform add android
ECHO.
CALL :INITPLUGINS

GOTO :EOF



:SERVEAPK
ECHO SERVING APK
START "Phonegap Live Server" phonegap serve --no-autoreload
GOTO :EOF

:BUILDAPK
ECHO BUILD APK
CALL phonegap build android --%_APK_MODE% --verbose
COPY "platforms\android\build\outputs\apk\android-debug.apk" %_APK%
CALL :UPDATE_APK
GOTO :EOF


:INSTAPK
ECHO UNINSTALL APK
CALL adb uninstall %_APK_PACKAGE%
ECHO.
ECHO INSTALL APK
CALL adb install -r %_APK%
GOTO :EOF


:RUNAPK
ECHO RUN APK
ECHO PACKAGE: %_APK_PACKAGE%/%_APK_ACTIVITY%
ECHO.
CALL adb shell am start -S -n %_APK_PACKAGE%/%_APK_ACTIVITY%
GOTO :EOF


:COMMAND
SET _COMMAND=
SET /P _COMMAND=%CD%^>
ECHO.
CALL %_COMMAND%
SET _COMMAND=
GOTO :EOF


:PROMPT
ECHO OPEN COMMAND PROMPT
START "" /I /MAX "%~d0\Libs\Setup.bat"
GOTO :EOF

:GITGUI
ECHO RUN GIT GUI
START "GIT PROMPT - DO NOT CLOSE" /MIN Git gui
GOTO :EOF


:HELP
ECHO Make sure to change the mode of the apk to match that on phonegap build.
ECHO.
ECHO Can enter multiple values when prefixing with '-' (i.e. -890)
ECHO EXAMPLE: -BUD90 (Builds resources, uploads, downloads, installs and runs on device)
GOTO :EOF

:RESTART
ENDLOCAL
START "" /I /MAX %CMDCMDLINE%

:QUIT
EXIT
